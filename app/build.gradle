/*
 * Copyright (c) 2022-2022 Balázs Püspök-Kiss (Illyan)
 *
 * Jay is a driver behaviour analytics app.
 *
 * This file is part of Jay.
 *
 * Jay is free software: you can redistribute it and/or modify it under the
 * terms of the GNU General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 * Jay is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
 * without even the implied warranty of MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE. See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with Jay.
 * If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'kotlin-android'
    id 'com.google.dagger.hilt.android'
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6' // Depends on your kotlin version
    id 'de.mannodermaus.android-junit5'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    compileSdk 33

    namespace 'illyan.jay'

    defaultConfig {
        applicationId "illyan.jay"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', "MapboxAccessToken", "\"$MAPBOX_ACCESS_TOKEN\""
            buildConfigField 'String', "MapboxDownloadsToken", "\"$MAPBOX_DOWNLOADS_TOKEN\""
            buildConfigField 'String', "MapboxSdkRegistryToken", "\"$SDK_REGISTRY_TOKEN\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "MapboxAccessToken", "\"$MAPBOX_ACCESS_TOKEN\""
            buildConfigField 'String', "MapboxDownloadsToken", "\"$MAPBOX_DOWNLOADS_TOKEN\""
            buildConfigField 'String', "MapboxSdkRegistryToken", "\"$SDK_REGISTRY_TOKEN\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.1"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
        animationsDisabled = true
    }
    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
    lint {
        // If set to true (default), stops the build if errors are found.
        abortOnError false
    }
}

dependencies {
    // Core
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.7.20'
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.collection:collection-ktx:1.2.0" // The Collection extensions contain utility functions for working with Android's memory-efficient collection libraries, including ArrayMap, LongSparseArray, LruCache, and others.
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation 'androidx.activity:activity-ktx:1.6.0'
    implementation "com.google.android.material:material:1.6.1"

    // Compose
    def activity_version = '1.6.0'
    def lifecycle_version = '2.5.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation 'androidx.compose.material3:material3:1.0.0-beta03'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:$activity_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.compose.foundation:foundation:$compose_version"

    // Material design icons
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // Layouts
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    // Mapbox
    implementation "com.mapbox.maps:android:10.8.0"
    implementation "com.mapbox.search:mapbox-search-android-ui:1.0.0-beta.35"
    implementation "com.mapbox.navigation:android:2.8.0"

    // Accompanist
    def accompanist_version = '0.26.5-rc'
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-placeholder-material:$accompanist_version"

    // Hilt
    def hilt_version = '2.44'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Navigation
    def compose_nav_version = '1.7.21-beta'
    implementation "io.github.raamcosta.compose-destinations:animations-core:$compose_nav_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_nav_version"

    // Networking
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    // Room
    def room_version = '2.4.3'
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version" // optional - RxJava2 support for Room
    implementation "androidx.room:room-rxjava3:$room_version" // optional - RxJava3 support for Room
    implementation "androidx.room:room-guava:$room_version" // optional - Guava support for Room, including Optional and ListenableFuture
    testImplementation "androidx.room:room-testing:$room_version" // optional - Test helpers
    implementation "androidx.room:room-paging:$room_version" // optional - Paging 3 Integration

    // Proto DataStore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.5"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'

    // Coroutine
    def coroutines_version = '1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Google services
    implementation 'com.google.firebase:firebase-firestore-ktx:24.3.1'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation 'com.google.android.gms:play-services-auth:20.3.0'
    implementation 'com.google.firebase:firebase-auth-ktx:21.0.8'
    implementation 'com.google.firebase:firebase-auth:21.0.8'
    implementation 'com.google.maps.android:android-maps-utils:2.4.0'
    // KTX for the Maps SDK for Android library
    implementation 'com.google.maps.android:maps-utils-ktx:3.4.0'
    // KTX for the Maps SDK for Android Utility Library

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:30.5.0')

    // Declare the dependencies for the Remote Config and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-config-ktx:21.1.2'
    implementation 'com.google.firebase:firebase-analytics-ktx:21.1.1'

    // JUnit5 testing tools
    def junit_version = '5.9.1'
    // (Required) Writing and executing Unit Tests on the JUnit Platform
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    // (Optional) If you need "Parameterized Tests"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    // (Optional) If you also have JUnit 4-based tests
    testImplementation "junit:junit:4.13.2"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit_version"

    // MockK test tool
    def mockk_version = '1.13.2'
    testImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"

    // Other testing tools
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"

    // Desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}